buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.bmuschko:gradle-docker-plugin:$dockerGradlePluginVersion"
        classpath "net.ltgt.gradle:gradle-apt-plugin:$aptGradlePluginVersion"
    }
}

allprojects {

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    version = projectVersion

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        options.warnings = false
    }

    apply plugin: "com.bmuschko.docker-remote-api"

    docker {
        // Set env variable to use Docker Toolbox
        if (System.env.DOCKER_HOST) {
            url = "$System.env.DOCKER_HOST".replace("tcp:", "https:")
            certPath = new File("$System.env.DOCKER_CERT_PATH")
        } else {
            // Or use Docker native through local socket
            url = "unix:///var/run/docker.sock"
        }
        registryCredentials {
            username = project.properties.dockerHubUsername
            password = project.properties.dockerHubPassword
        }
    }

    ext.dockerImageName = "$dockerRepository/$project.name:$imageVersion".toString()

    apply plugin: 'eclipse'
}
